buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:6.1.1'
    }
}

plugins {
    id 'application'
    id 'distribution'
    id 'nebula.rpm' version '6.1.1'
}

mainClassName = 'org.janelia.jacsstorage.client.StorageClientApp'

project(":jacsstorage-clients") {
    dependencies {
        implementation project(":jacsstorage-core"),
                       CDI_API_LIB,
                       CDI_SE_CORE_LIB,
                       CMDLINE_ARG_LIB,
                       JAX_RS_JERSEY_CLIENT_LIB,
                       JAX_RS_JERSEY_INJECT_LIB,
                       JAX_RS_JERSEY_MEDIA_JSON_LIB

        perfAnnotationProcessor BENCHMARK_ANN_PROCESSOR_LIB

        perfImplementation project
    }

    task createStorageClientApp(type: CreateStartScripts) {
        mainClassName = "org.janelia.jacsstorage.client.StorageClientApp"
        classpath = startScripts.classpath
        outputDir = startScripts.outputDir
        applicationName = "storageClientApp"
    }

    task createStoragePersistBenchmark(type: CreateStartScripts, dependsOn: perfJar) {
        mainClassName = "org.janelia.jacsstorage.benchmarks.StoragePersistBenchmark"
        classpath = startScripts.classpath + configurations.perfRuntimeClasspath + project.perfJar.outputs.files
        outputDir = startScripts.outputDir
        applicationName = "storagePersistBenchmarks"
    }

    task createStorageRetrieveBenchmark(type: CreateStartScripts, dependsOn: perfJar) {
        mainClassName = "org.janelia.jacsstorage.benchmarks.StorageRetrieveBenchmark"
        classpath = startScripts.classpath + configurations.perfRuntimeClasspath + project.perfJar.outputs.files
        outputDir = startScripts.outputDir
        applicationName = "storageRetrieveBenchmarks"
    }

    task createStorageUpdateBenchmark(type: CreateStartScripts, dependsOn: perfJar) {
        mainClassName = "org.janelia.jacsstorage.benchmarks.StorageUpdateBenchmark"
        classpath = startScripts.classpath + configurations.perfRuntimeClasspath + project.perfJar.outputs.files
        outputDir = startScripts.outputDir
        applicationName = "storageUpdateBenchmarks"
    }

    distributions {
        benchmarks {
            contents {
                into("bin") {
                    from(createStoragePersistBenchmark)
                    from(createStorageRetrieveBenchmark)
                    from(createStorageUpdateBenchmark)
                    fileMode = 0755
                }
                into("lib") {
                    from(project.perfJar.outputs.files)
                    from(project.configurations.perfRuntimeClasspath)
                }
            }
        }
        main {
            contents {
                into("bin") {
                    from(createStorageClientApp)
                    fileMode = 0755
                }
            }
        }
    }

    task ospackage(type: Rpm, dependsOn: installDist) {
        release = "1"
        arch = I386
        os = LINUX

        def rpmInstallDir;

        if (project.hasProperty('jacs.runtime.env.jadeToolsDir')) {
            rpmInstallDir = project.getProperty("jacs.runtime.env.jadeToolsDir") + "-${version}"
        } else {
            rpmInstallDir= "/misc/sc/jadeTools-${version}"
        }
        def parentRpmInstallDir = new File(rpmInstallDir).parent
        def installEnv = ["jacs.runtime.env.installDir": rpmInstallDir]

        into "${rpmInstallDir}"

        from ("${buildDir}/install/${project.name}/bin") {
            into "bin"
            filter(org.apache.tools.ant.filters.ExpandProperties, project: filteringProperties(installEnv))
        }

        from ("${buildDir}/install/${project.name}/lib") {
            into "lib"
        }

        from ("src/main/install/scripts") {
            into "bin"
            filter(org.apache.tools.ant.filters.ExpandProperties, project: filteringProperties(installEnv))
            fileMode = 0755
        }

        from ("src/main/install/etc") {
            into "etc"
            filter(org.apache.tools.ant.filters.ExpandProperties, project: filteringProperties(installEnv))
        }

        link("${parentRpmInstallDir}/jadeTools", "${rpmInstallDir}")
    }
}

ext.moduleName = 'org.janelia.jacsstorage.clients'
