allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    group = 'org.janelia.jacsstorage'
    version = '1.7.2.RELEASE'
}

subprojects {
    apply from: "${rootDir}/deps.gradle"

    configurations {
        provided
        implementation.extendsFrom(provided)
        integrationTestCompileOnly.extendsFrom(testCompileOnly)
        integrationTestImplementation.extendsFrom(testImplementation)
        integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
        perf
        perfApi.extendsFrom(api)
        perfCompileOnly.extendsFrom(compileOnly)
        perfImplementation.extendsFrom(perfApi, implementation)
        perfRuntimeOnly.extendsFrom(runtimeOnly, perfImplementation)
    }

    sourceSets {
        perf {
            java {
                runtimeClasspath += perf.output
            }
        }
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integrationTest/java')
            }
            resources {
                srcDir file('src/integrationTest/resources')
            }
        }
    }

    dependencies {
        perfImplementation BENCHMARK_CORE_LIB,
                           BENCHMARK_ANN_PROCESSOR_LIB
    }

    afterEvaluate {
        repositories {
            mavenLocal()
            mavenCentral()
        }

        compileJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        compileTestJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        compileIntegrationTestJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        compilePerfJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        idea {
            module {
                downloadSources = true
                downloadJavadoc = true
                scopes.TEST.plus += [
                    configurations.integrationTestCompileOnly,
                    configurations.integrationTestRuntime
                ]
            }
        }

   }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task perfJar(type: Jar, dependsOn: configurations.perf) {
        from sourceSets.perf.output
        classifier = "perf"
    }

    task integrationTest(type: Test) {
        testClassesDirs += sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
    }

    artifacts {
        perf perfJar
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}

task updateWrapper(type: Wrapper) {
    gradleVersion = "5.0"
}

def filteringProperties(otherProperties) {
        def tmpProject = new org.apache.tools.ant.Project()
        tmpProject.initProperties()
        // convert the build config to ant properties to be used with the ant ExpandProperties
        project.ext.properties.each { k,v ->
                if (k.startsWith("jacs.runtime.env.")) {
                        tmpProject.setProperty("${k}", "${v}")
                }
        }
        otherProperties.each { k,v ->
                if (k.startsWith("jacs.runtime.env.")) {
                        tmpProject.setProperty("${k}", "${v}")
                }
        }
        tmpProject
}
