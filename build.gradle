allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    group = 'org.janelia.jacsstorage'
    version = '1.13.9.RELEASE'
}

subprojects {
    configurations {
        provided
        implementation.extendsFrom(provided)
        integrationTestCompileOnly.extendsFrom(testCompileOnly)
        integrationTestImplementation.extendsFrom(testImplementation)
        integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
        perfApi.extendsFrom(api)
        perfImplementation.extendsFrom(perfApi, implementation)
        perfRuntimeOnly.extendsFrom(perfImplementation, runtimeOnly)
    }

    apply from: "${rootDir}/deps.gradle"

    sourceSets {
        perf {
            java {
                runtimeClasspath += main.output + perf.output
                srcDir file('src/perf/java')
            }
            resources {
                srcDir file('src/perf/resources')
            }
        }
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integrationTest/java')
            }
            resources {
                srcDir file('src/integrationTest/resources')
            }
        }
    }

    afterEvaluate {
        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                url "https://nexus.janelia.org/repository/maven-releases"
            }
            maven {
                url "https://nexus.janelia.org/repository/maven-snapshots"
            }
        }

        compileJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        compileTestJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        compileIntegrationTestJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        compilePerfJava {
            doFirst {
                options.compilerArgs = [
                        '-Xlint:deprecation',
                        '-Xlint:unchecked'
                ]
            }
        }

        idea {
            module {
                downloadSources = true
                downloadJavadoc = true
                scopes.TEST.plus += [
                    configurations.integrationTestCompileOnly,
                    configurations.integrationTestRuntime,
                    configurations.perfApi,
                    configurations.perfRuntime
                ]
            }
        }

   }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task perfJar(type: Jar, dependsOn: configurations.perfRuntime) {
        from sourceSets.perf.output
        classifier = "perf"
    }

    task integrationTest(type: Test) {
        testClassesDirs += sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
    }

    artifacts {
        perfRuntime perfJar
    }
}

task updateWrapper(type: Wrapper) {
    gradleVersion = "5.1.1"
}

def filteringProperties(otherProperties) {
        def tmpProject = new org.apache.tools.ant.Project()
        tmpProject.initProperties()
        // convert the build config to ant properties to be used with the ant ExpandProperties
        project.ext.properties.each { k,v ->
                if (k.startsWith("jacs.runtime.env.")) {
                        tmpProject.setProperty("${k}", "${v}")
                }
        }
        otherProperties.each { k,v ->
                if (k.startsWith("jacs.runtime.env.")) {
                        tmpProject.setProperty("${k}", "${v}")
                }
        }
        tmpProject
}
