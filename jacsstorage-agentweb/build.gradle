buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:4.4.0'
    }
}

apply plugin: 'application'
apply plugin: 'distribution'
apply plugin: 'nebula.rpm'
 
mainClassName = 'org.janelia.jacsstorage.app.JacsAgentStorageApp'

project(":jacsstorage-agentweb") {
    dependencies {
        implementation project(":jacsstorage-commonweb"),
                       CDI_SE_CORE_LIB,
                       JAX_RS_CLIENT_LIB,
                       JERSEY_CDI_SERVLET_LIB,
                       JERSEY_INJECT_LIB,
                       JERSEY_MEDIA_JSON_LIB

        testImplementation JERSEY_TEST_LIB,
                           JERSEY_TEST_PROVIDER_LIB

    }

    distributions {
        main {
	    contents {
                into('swagger-webapp') {
                    from(new File(project(':jacsstorage-commonweb').projectDir, 'src/main/swagger-webapp')) {
		        include '**'
		    }
                    from(new File(projectDir, 'src/main/swagger-webapp')) {
		        include 'index.html'
		    }
                }
	    }
	}
    }

    task packageRpm(type: Rpm, dependsOn: distTar) {
        release = "1"
        arch = I386
        os = LINUX

        def rpmInstallDir = "/opt/servers/jacsstorage/${project.name}-${version}"
        def installEnv = ["jacs.runtime.env.installDir": rpmInstallDir]

        into "${rpmInstallDir}"

        from(new File(project(':jacsstorage-commonweb').projectDir, 'src/main/install/etc')) {
            into "etc"
            filter(org.apache.tools.ant.filters.ExpandProperties, project: filteringProperties(installEnv))	
        }

        from ("${buildDir}/scripts") {
            into "bin"
            filter(org.apache.tools.ant.filters.ExpandProperties, project: filteringProperties(installEnv))
	}

        from ("src/main/install/bin") {
            into "bin"
            filter(org.apache.tools.ant.filters.ExpandProperties, project: filteringProperties(installEnv))	
	}

        from ("src/main/install/etc") {
            into "etc"
            filter(org.apache.tools.ant.filters.ExpandProperties, project: filteringProperties(installEnv))	
	}

        from ("${buildDir}/libs") {
            into "libs"
	}

        link("/etc/systemd/system/${project.name}.service", "${rpmInstallDir}/etc/${project.name}.service")
    }
}

ext.moduleName = 'org.janelia.jacsstorage.agentweb'
